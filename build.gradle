plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id 'com.github.ben-manes.versions' version '0.22.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.21'
}

group 'uk.gov.hmcts.reform'
version '0.0.3'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

def springBootVersion = '2.1.6.RELEASE'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.24'
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/fees-java-client"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

ext {
    lombokVersion = '1.18.8'
    springCloudVersion = 'Greenwich.SR2'
    feignVersion = '10.2.3'
}

def versions = [
    logback           : '1.2.3',
    springBoot        : springBootVersion,
    reformJavaLogging : '5.0.1',
    junitJupiter      : '5.5.1'
]

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task functionalTest(type: Test) {
    description = 'Runs functional tests.'
    group = 'verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.9'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
    compile group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformJavaLogging

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junitJupiter
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    testImplementation 'ru.lanwen.wiremock:wiremock-junit5:1.3.0'

    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.24.1'
    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'

    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'fees-client'
            version project.version

            pom {
                name = 'Fees java client'
                description = 'Client library for communicating with fees api'
                url = gitRepo
                licenses {
                    license {
                        name = 'MIT License, Copyright (c) 2019 HM Courts & Tribunals Service'
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                scm {
                    url = gitRepo
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'fees-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = gitRepo
        version {
            name = project.version
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}
